# zsh prompt setup

function short_pwd {
    local current_dir="${1:-${PWD}}"
    current_dir="${current_dir/#${HOME}/~}"

    if [[ ${current_dir} != '~' ]]; then
        print "${${${${(@j:/:M)${(@s:/:)current_dir}##.#?}:h}%/}//\%/%%}/${${current_dir:t}//\%/%%}"
    else
        print '~'
    fi
}

function status {
    local error_colour="%F{red}"
    local colour_1="%(0?.%F{red}.$error_colour)"
    local colour_2="%(0?.%F{yellow}.$error_colour)"
    local colour_3="%(0?.%F{green}.$error_colour)"

    print "${colour_1}❯${colour_2}❯${colour_3}❯%f"
}

function path {
    print "%F{blue}$(sed s_/_/_g <<< $(short_pwd))%f"
}

function vim_status {
    if [[ "$KEYMAP" == 'vicmd' ]]; then
        print "%F{yellow} [% NORMAL]% %f"
    fi
}

function zle-line-init zle-line-finish zle-keymap-select {
    zle reset-prompt
    zle -R
}

function prompt_precmd {
    zle -N zle-line-init
    zle -N zle-keymap-select
    zle -N zle-line-finish

    PROMPT='$(path) $(status) '
    RPROMPT='$(vim_status)'
}

if [[ ! ${TERM} == (linux|*bsd*|dumb) ]]; then
    autoload -Uz promptinit && promptinit
    autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_precmd
    prompt_opts=(cr subst percent)
fi

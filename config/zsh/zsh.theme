# My minimal theme

minimal_status() {
    local error_colour="%F{red}"
    local colour_1="%(0?.%F{red}.$error_colour)"
    local colour_2="%(0?.%F{yellow}.$error_colour)"
    local colour_3="%(0?.%F{green}.$error_colour)"

    print "${colour_1}❯${colour_2}❯${colour_3}❯%f"
}

minimal_path() {
    local path_color="%F{blue}"
    local rsc="%f"
    local sep="/"

    print "$path_color$(sed s_/_${sep}_g <<< $(short_pwd))$rsc"
}

minimal_vim_status() {
    local vim_prompt="%F{yellow} [% NORMAL]% %f"
    print "${${KEYMAP/vicmd/$vim_prompt}/(main|viins)/}"
}

git_branch_name() {
    local branch_name="$(command git rev-parse --abbrev-ref HEAD 2> /dev/null)"
    [[ -n $branch_name ]] && print "$branch_name"
}

git_repo_status(){
    local rs="$(command git status --porcelain -b)"

    if $(print "$rs" | grep -v '^##' &> /dev/null); then # is dirty
        print "%F{red}"
    elif $(print "$rs" | grep '^## .*diverged' &> /dev/null); then # has diverged
        print "%F{red}"
    elif $(print "$rs" | grep '^## .*behind' &> /dev/null); then # is behind
        print "%F{yellow}"
    elif $(print "$rs" | grep '^## .*ahead' &> /dev/null); then # is ahead
        print "%f"
    else # is clean
        print "%F{green}"
    fi
}

minimal_git() {
    local bname=$(git_branch_name)
    if [[ -n ${bname} ]]; then
        local info="$(git_repo_status)${bname}%f"
        print "$info"
    fi
}

function zle-line-init zle-line-finish zle-keymap-select {
    zle reset-prompt
    zle -R
}

prompt_minimal_precmd() {
    zle -N zle-line-init
    zle -N zle-keymap-select
    zle -N zle-line-finish

    PROMPT='$(minimal_path) $(minimal_status) '
    RPROMPT='$(minimal_vim_status)'
}

prompt_minimal_setup() {
    autoload -Uz add-zsh-hook

    add-zsh-hook precmd prompt_minimal_precmd
    prompt_opts=(cr subst percent)
}

prompt_minimal_setup "$@"

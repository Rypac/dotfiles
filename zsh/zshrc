[ -f "$HOME/.profile" ] && source "$HOME/.profile"

: ${XDG_CACHE_HOME:="$HOME/.cache"}
: ${XDG_DATA_HOME:="$HOME/.local/share"}

: ${ZPLUG_HOME:="$XDG_DATA_HOME/zplug"}
: ${ZPLUG_BIN:="$ZPLUG_HOME/bin"}
: ${ZPLUG_CACHE_DIR:="$XDG_CACHE_HOME/zplug"}

export TERM='xterm-256color'


## Plugins

if [ ! -f "$ZPLUG_HOME/init.zsh" ]; then
    git clone --depth 1 "https://github.com/zplug/zplug" "$ZPLUG_HOME"
fi

source "$ZPLUG_HOME/init.zsh"

zplug "zplug/zplug", hook-build:"zplug --self-manage"
zplug "zsh-users/zsh-completions", depth:1
zplug "zsh-users/zsh-autosuggestions"
zplug "zsh-users/zsh-syntax-highlighting", defer:2
zplug "zsh-users/zsh-history-substring-search", defer:2
zplug "b4b4r07/zsh-vimode-visual", defer:3
zplug "junegunn/fzf", depth:1, as:command, use:bin/fzf, hook-build:"./install --bin"
zplug "b4b4r07/enhancd", use:init.sh
zplug "tj/n", as:command, use:bin/n
zplug "rupa/z", use:z.sh
zplug "chriskempson/base16-shell", use:scripts/base16-oceanicnext.sh

if ! zplug check; then
    zplug install
fi

zplug load


## Options

HISTFILE="$XDG_CACHE_HOME/.zhistory"
HISTSIZE=10000
SAVEHIST=10000
setopt EXTENDED_HISTORY     # prepend timestamp to each history entry
setopt INC_APPEND_HISTORY   # history entries are appended after each command
setopt SHARE_HISTORY        # imports commands from and writes commands to the history file
setopt HIST_IGNORE_DUPS     # don't add command to history if it's a duplicate of the previous entry
setopt HIST_IGNORE_ALL_DUPS # remove previous entry from history and append command to the end
setopt HIST_IGNORE_SPACE    # ignore commands prepended with a space
setopt HIST_SAVE_NO_DUPS    # older history commands are removed in favour of newer ones
setopt HIST_VERIFY          # perform history expansion and reload line into the editing buffer
setopt AUTO_PUSHD           # cd pushes old directory to the stack
setopt PUSHD_IGNORE_DUPS    # push only unique directories
setopt PUSHD_SILENT         # don't print directory stach when pushing or popping
setopt PUSHD_TO_HOME        # push with no arguements pushes to home
setopt EXTENDED_GLOB        # treat '#', '~' and '^' as part of patterns for filename generation
setopt PROMPT_SUBST         # substitutions can be used in the prompt
setopt AUTO_RESUME          # single work commands can resume existing jobs
setopt LONG_LIST_JOBS       # list jobs in the long format by default
setopt ALWAYS_TO_END        # move cursor to end of word when inserting completion
setopt PATH_DIRS            # perform path search completion on commands with slashes
unsetopt CASE_GLOB          # don't make globbing completion case sensitive
unsetopt MENU_COMPLETE      # don't insert first completion match immediately


## Completions

zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:options' auto-description '%d'
zstyle ':completion:*:corrections' format ' %F{green}-- %d (errors: %e) --%f'
zstyle ':completion:*:descriptions' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'
zstyle ':completion:*:default' list-prompt '%S%M matches%s'
zstyle ':completion:*' format ' %F{yellow}-- %d --%f'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' verbose yes
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:*:cd:*' tag-order local-directories directory-stack path-directories
zstyle ':completion:*:*:cd:*:directory-stack' menu yes select
zstyle ':completion:*:-tilde-:*' group-order 'named-directories' 'path-directories' 'expand'
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "$XDG_CACHE_HOME/.zcompcache"
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec)|prompt_*)'
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true
zstyle ':completion:*:history-words' stop yes
zstyle ':completion:*:history-words' remove-all-dups yes
zstyle ':completion:*:history-words' list false
zstyle ':completion:*:history-words' menu yes
autoload -Uz compinit && compinit -d "$XDG_CACHE_HOME/.zcompcache"


## Keybindings

bindkey -v  # Vim mode
export KEYTIMEOUT=10

bindkey -M viins ' ' magic-space
bindkey -M viins '^[[3~' delete-char
bindkey -M viins '^?' backward-delete-char
bindkey -M viins '^[[D' backward-char
bindkey -M viins '^[[C' forward-char
bindkey -M viins '^[[7~' beginning-of-line
bindkey -M viins '^[[8~' end-of-line
bindkey -M viins '^[[2~' overwrite-mode
bindkey -M viins '[D' backward-word
bindkey -M viins '[C' forward-word
bindkey -M viins '^[[1;5D' backward-word
bindkey -M viins '^[[1;5C' forward-word
bindkey -M viins '^[Od' backward-word
bindkey -M viins '^[Oc' forward-word
bindkey -M viins '^[[3^' kill-word
bindkey -M viins '^H' backward-kill-word
bindkey -M viins '^A' beginning-of-line
bindkey -M viins '^E' end-of-line
bindkey -M viins '^L' clear-screen
bindkey -M viins '^K' kill-line
bindkey -M viins '^U' backward-kill-line
bindkey -M viins '^F' vi-cmd-mode
bindkey -M viins '^B' push-line-or-edit
bindkey -M viins '^I' expand-or-complete
bindkey -M viins '^Y' yank
bindkey -M viins '^P' history-substring-search-up
bindkey -M viins '^N' history-substring-search-down
bindkey -M viins '^[[Z' reverse-menu-complete
bindkey -M viins '^[[A' history-substring-search-up
bindkey -M viins '^[OA' history-substring-search-up
bindkey -M viins '^[[B' history-substring-search-down
bindkey -M viins '^[OB' history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down
bindkey -M vicmd '/' history-incremental-search-backward
bindkey -M vicmd '?' history-incremental-search-forward


## Prompt

function left_prompt {
    local current_directory="%F{blue}%1~"
    local command_status="%(0?.%F{green}.%F{red})$%f"
    print "$current_directory $command_status "
}

function right_prompt {
    if [ "$KEYMAP" = 'vicmd' ]; then
        print "%F{yellow} [% NORMAL]% %f"
    elif [ "$KEYMAP" = 'vivis' ]; then
        print "%F{yellow} [% VISUAL]% %f"
    fi
}

function zle-line-init zle-line-finish zle-keymap-select {
    zle reset-prompt
    zle -R
}

function prompt_precmd {
    zle -N zle-line-init
    zle -N zle-keymap-select
    zle -N zle-line-finish
    PROMPT='$(left_prompt)'
    RPROMPT='$(right_prompt)'
}

if [[ ! "$TERM" == (linux|*bsd*|dumb) ]]; then
    autoload -Uz promptinit && promptinit
    autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_precmd
    prompt_opts=(cr subst percent)
fi

typeset -gU PATH FPATH CDPATH MAILPATH


## Aliases

# Neovim
alias vi='nvim'
alias vim='nvim'

# tmux
alias mux='tmuxinator'
function tm {
    name="${1:-default}"
    tmux attach -t "$name" || tmux new -s "$name"
}

# Docker
alias docker-stop='docker stop $(docker ps -aq)'
alias docker-clean='docker rm -f $(docker ps -aq)'
alias docker-purge='docker rmi -f $(docker images -aq)'

# VSCode
alias vscode='code'

alias grep='grep --color=auto'
alias cpr='cp -r'
alias rmr='rm -rf'
alias mkp='mkdir -p'
alias l='ls -lAh'
alias la='ls -lAh'
alias ll='ls -lh'
alias lr='ll -R'

if [ "$(uname)" = "Linux" ]; then
    # Python
    alias pip-upgrade="pip3 list --outdated --user | cut -d ' ' -f 1 | xargs -n 1 pip3 install --upgrade --user"

    # VSCode
    alias code="code --user-data-dir "$XDG_CONFIG_HOME/vscode" --extensions-dir "$XDG_CONFIG_HOME/vscode/extensions""

    # Clipboard
    alias setclip='xclip -selection clipboard -i'
    alias getclip='xclip -selection clipboard -o'

    # Aliases
    function f {
        (pcmanfm "${1:-.}" > /dev/null 2>&1 &)
    }

    # Modifying permissions
    alias chmod='chmod --preserve-root -v'
    alias chown='chown --preserve-root -v'

    # ls colours
    if type dircolors > /dev/null 2>&1; then
        eval "$(dircolors --sh)"
    fi
    alias ls='ls --color=auto'

    # Package manager
    alias appupdate='sudo apt update && sudo apt upgrade'
    alias appinstall='sudo apt install'
    alias appremove='sudo apt remove'
    alias apppurge='sudo apt purge --autoremove'
    alias appsearch='sudo apt search'
    alias appclean='sudo apt autoremove'

    function appcleanall {
        sudo dpkg -P $(dpkg -l | awk '/^rc/ { print($2) }')
    }
    function applist {
        dpkg-query -f '${binary:Package}\n' -W | less
    }
elif [ "$(uname)" = "Darwin" ]; then
    # Ruby
    alias be='bundle exec'

    # Python
    alias pip-upgrade="pip3 list --outdated | cut -d ' ' -f 1 | xargs -n 1 pip3 install --upgrade"

    # VSCode
    alias vscode='code'

    # Vim (work around tmux issue on macOS sierra)
    alias nvim='reattach-to-user-namespace -l nvim'

    # Aliases
    alias f='open -a Finder ./'

    # Package manager
    alias appupdate='brew update && brew upgrade'
    alias appinstall='brew install'
    alias appremove='brew uninstall'
    alias appsearch='brew search'
    alias appclean='brew cleanup && brew cask cleanup && brew prune'
    alias appdoctor='brew doctor && brew cask doctor'

    alias ls='ls -G'
fi
